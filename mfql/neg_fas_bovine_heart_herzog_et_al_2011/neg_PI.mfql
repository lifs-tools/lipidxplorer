##########################################################
# Identify PI with checking  the precursor mass          #
##########################################################

QUERYNAME = Phosphatidylinositol;
DEFINE PR = 'C[37..53] H[30..140] O[13] P[1]' WITH DBR = (3.5,10.5), CHG = -1;
DEFINE headPI = 'C[6] H[10] O[8] P[1]' WITH DBR = (1.5,4.5), CHG = -1;
DEFINE FA1 ='C[14..22] H[20..50] O[2]' WITH DBR = (1.5,7.5), CHG = -1;
DEFINE FA2 ='C[14..22] H[20..50] O[2]' WITH DBR = (1.5,7.5), CHG = -1;

IDENTIFY Phosphatidylinositol WHERE
	
	# marking
	PR IN MS1- AND
	FA1 in MS2- AND
	FA2 in MS2- AND
	headPI in MS2- 

SUCHTHAT
        isOdd(PR.chemsc[C]) AND
	isEven(FA1.chemsc[C]) AND
	isEven(FA2.chemsc[C]) AND
	FA1.chemsc + FA2.chemsc + headPI.chemsc + 'C3 H6 O1' == PR.chemsc
  
REPORT 
	MASS = "%4.4f" % "(PR.mass)";
	CHEMSC = PR.chemsc;
        ERROR = "%2.2fppm" % "(PR.errppm)";
	NAME = "PI [%d:%d]" % "((PR.chemsc)[C] - 9, (PR.chemsc)[db] - 3.5)";
	SPECIE = "PI [%d:%d / %d:%d]" % "(FA1.chemsc[C], FA1.chemsc[db] - 1.5, FA2.chemsc[C], FA2.chemsc[db] - 1.5)";
	PRECURINTENS = PR.intensity;
	NLSPIS = headPI.intensity;
	FAS = sumIntensity(FA1.intensity, FA2.intensity);;

################ end script ##################
