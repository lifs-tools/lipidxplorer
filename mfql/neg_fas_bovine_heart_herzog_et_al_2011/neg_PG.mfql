##########################################################
# Identify PG with checking  the precursor mass and FAS         #
##########################################################

QUERYNAME = Phosphatidylglycerol;
DEFINE PR = 'C[34..50] H[30..120] O[10] P[1]' WITH DBR = (2.5,8.5), CHG = -1;
DEFINE FA1 ='C[14..22] H[20..50] O[2]' WITH DBR = (1.5,7.5), CHG = -1;
DEFINE FA2 ='C[14..22] H[20..50] O[2]' WITH DBR = (1.5,7.5), CHG = -1;

IDENTIFY Phosphatidylglycerol WHERE
	
	# marking
	PR IN MS1-  AND
	FA1 in MS2- AND
	FA2 in MS2- 

SUCHTHAT
        isEven(PR.chemsc[C]) AND
	isEven(FA1.chemsc[C]) AND
	isEven(FA2.chemsc[C]) AND
	FA1.chemsc + FA2.chemsc + 'C6 H12 P1 O6' == PR.chemsc
  
REPORT 
	MASS = "%4.4f" % "(PR.mass)";
	CHEMSC = PR.chemsc;
        ERROR = "%2.2fppm" % "(PR.errppm)";
	NAME = "PG [%d:%d]" % "((PR.chemsc)[C] - 6, (PR.chemsc)[db] - 2.5)";
	SPECIE = "PG [%d:%d / %d:%d]" % "(FA1.chemsc[C], FA1.chemsc[db] - 1.5, FA2.chemsc[C], FA2.chemsc[db] - 1.5)";
	PRECURINTENS = PR.intensity;
	FAS = sumIntensity(FA1.intensity, FA2.intensity);;

################ end script ##################
